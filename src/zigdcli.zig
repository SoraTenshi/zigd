const std = @import("std");
const zigdcore = @import("zigdcore.zig");
const utils = @import("utils.zig");

const Command = enum {
    install,
    setup,
    exists,
    help,
    version,
};

pub fn main() !void {
    var gpa = if (utils.InDebug) std.heap.GeneralPurposeAllocator(.{}){};
    const allocator = if (utils.InDebug) gpa.allocator() else std.heap.c_allocator;
    defer _ = if (utils.InDebug) gpa.deinit();

    const args = try std.process.argsAlloc(allocator);
    defer std.process.argsFree(allocator, args);

    std.debug.assert(args.len >= 1);

    if (args.len == 1)
        return try help_menu();

    if (std.meta.stringToEnum(Command, args[1])) |command| {
        return try switch (command) {
            .install => install(allocator, args),
            .setup => setup(allocator, args),
            .exists => exists(allocator, args),
            .help => help_menu(),
            .version => version(),
        };
    }

    std.log.err("Unkown command entered!\n", .{});
    return try help_menu();
}

fn help_menu() !void {
    try std.io.getStdOut().writer().print(
        \\> zigd ({s}) cli: Manage zigd stuff
        \\ 
        \\install [version] - Install a zig version
        \\setup [version] - First time setup (creates a config and installs the version)
        \\exists [version] - Check if a zig version is installed on the system
        \\help - Outputs this help Menu
        \\version - Outputs zigd version
        \\
    , .{utils.zigd_version});
    return;
}

fn version() !void {
    try std.io.getStdOut().writer().print("{s}\n", .{utils.zigd_version});
    return;
}

var user_arg: zigdcore.ZigVersion.Source = .UserArg;

fn install(allocator: std.mem.Allocator, args: []const []const u8) !void {
    if (args.len <= 2) {
        std.log.err("Wrong Usage!\n", .{});
        try std.io.getStdOut().writer().print(
            \\Usage: zigd install [version]
            \\For more help use zigd help
            \\
        , .{});
        return;
    }

    var zig_version = try zigdcore.ZigVersion.parse(allocator, args[2], &user_arg, false);
    defer zig_version.deinitIfMasterOrZigverOrZonver(allocator);

    try std.io.getStdOut().writer().print("Installing zig version {s}\n", .{zig_version});

    const download_url = try zigdcore.downloadUrlFromVersion(allocator, zig_version.as_string, zig_version.source == .Master);
    defer allocator.free(download_url);

    const zigd_path = try zigdcore.getZigdPath(allocator);
    defer allocator.free(zigd_path);

    if (!try zigdcore.install_zig(allocator, download_url, zigd_path, zig_version)) {
        std.log.err("Installation failed!", .{});
    }

    return;
}

fn setup(allocator: std.mem.Allocator, args: []const []const u8) !void {
    if (args.len <= 2) {
        std.log.err("Wrong Usage!\n", .{});
        try std.io.getStdOut().writer().print(
            \\Usage: zigd setup [version]
            \\For more help use zigd help
            \\
        , .{});
        return;
    }

    const zigd_path = try zigdcore.getZigdPath(allocator);
    defer allocator.free(zigd_path);

    const config_path = try std.fs.path.join(allocator, &.{ zigd_path, "config" });
    defer allocator.free(config_path);

    if (try utils.isFile(config_path)) {
        o: while (true) {
            std.log.warn("A config file already exists, overwrite (y/n?)", .{});
            const byte = try std.io.getStdIn().reader().readByte();

            switch (byte) {
                'y' => break :o,
                'n' => return,
                else => continue :o,
            }
        }
    }

    var zig_version = try zigdcore.ZigVersion.parse(allocator, args[2], &user_arg, false);
    defer zig_version.deinitIfMasterOrZigverOrZonver(allocator);

    if (zig_version.source == .Master) {
        o: while (true) {
            std.log.warn("It is not recommended to setup with master, are you sure you want to set master as default in the config? (y/n)", .{});
            const byte = try std.io.getStdIn().reader().readByte();

            switch (byte) {
                'y' => break :o,
                'n' => return,
                else => continue :o,
            }
        }
    }

    const download_url = try zigdcore.downloadUrlFromVersion(allocator, zig_version.as_string, zig_version.source == .Master);
    defer allocator.free(download_url);

    try std.io.getStdOut().writer().print("Installing version {s}...\n", .{zig_version});
    _ = try zigdcore.install_zig(allocator, download_url, zigd_path, zig_version);

    try std.io.getStdOut().writer().print("Creating a config...\n", .{});
    const config_file = try std.fs.createFileAbsolute(config_path, .{
        .truncate = true,
    });
    defer config_file.close();

    try config_file.writer().writeAll("# Generated by `zigd setup`, btw :)\n");
    try config_file.writer().writeAll("default=");
    switch (zig_version.source) {
        .Master => try config_file.writeAll("master"),
        else => try config_file.writeAll(zig_version.as_string),
    }
    try config_file.writer().writeByte('\n');
}

fn exists(allocator: std.mem.Allocator, args: []const []const u8) !void {
    if (args.len <= 2) {
        std.log.err("Wrong Usage!\n", .{});
        try std.io.getStdOut().writer().print(
            \\Usage: zigd exists [version]
            \\For more help use zigd help
            \\
        , .{});
        return;
    }

    const zigd_path = try zigdcore.getZigdPath(allocator);
    defer allocator.free(zigd_path);

    var zig_version = try zigdcore.ZigVersion.parse(allocator, args[2], &user_arg, false);
    defer zig_version.deinitIfMasterOrZigverOrZonver(allocator);

    const version_path = try std.fs.path.join(allocator, &.{ zigd_path, "versions", args[2] });
    defer allocator.free(version_path);

    if (try utils.isDirectory(version_path)) {
        try std.io.getStdOut().writer().writeAll("Yes!\n");
    } else {
        try std.io.getStdOut().writer().writeAll("No!\n");
    }
}
